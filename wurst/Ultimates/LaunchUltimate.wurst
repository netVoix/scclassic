package LaunchUltimate

import RegisterEvents
import PrecisionUW
import ClosureTimers
import InstantDummyCaster
import Orders
import OverchargedUW
import Filters
import GlobalUW
import FocusedUW
import ObjectIdGenerator
import AbilityObjEditing
import Icons

let ULTIMATE_DAMAGE_SPELL_ID = compiletime(ABIL_ID_GEN.next())

@compiletime function create_w3a_A0OV()
    new AbilityDefinitionFarseerChainLightning(ULTIMATE_DAMAGE_SPELL_ID)
    ..setDummyAbility()
    ..setLevels(3)
    ..setHeroAbility(false)
    ..setLevelSkipRequirement(4)
    ..setHotkeyLearn("")
    ..presetTooltipLearn(lvl -> "")
    ..setTooltipLearnExtended("")
    ..presetTooltipNormal(lvl -> "UW")
    ..presetTooltipNormalExtended(lvl -> "Struck lightning to one building.")
    ..setButtonPositionNormalX(2)
    ..presetTargetsAllowed(lvl -> "structure")
    ..setIconNormal(Icons.bTNChainLightning)
    ..setMissileArt("")
    ..setMissileSpeed(0)
    ..setArtTarget("Lightnings Long.mdx")
    ..setName("UW")
    ..setLightningEffects("")
    ..setAnimationNames("spell")
    ..setRequirements("")
    ..presetDamageReductionperTarget(lvl -> 0)
    ..presetDamageperTarget(lvl -> 4500. + lvl * 1000)
    ..presetNumberofTargetsHit(lvl -> 1)
    ..setTargetAttachmentPoint("sprite,first")

init
    registerPlayerUnitEvent(EVENT_PLAYER_UNIT_SPELL_EFFECT) ->
        if GetSpellAbilityId() == PRECISION_UW_ID
            let u = GetTriggerUnit()
            let owner = u.getOwner()
            let t = GetSpellTargetUnit()
            QuestMessageBJ(bj_FORCE_ALL_PLAYERS, bj_QUESTMESSAGE_WARNING, owner.getNameColored() + " has fired Ultimate weapon!")
            doAfter(3) ->
                InstantDummyCaster.castTarget(owner, ULTIMATE_DAMAGE_SPELL_ID, 3, OrderIds.chainlightning, t)
                
        if GetSpellAbilityId() == OVERCHARGED_UW_ID
            let owner = GetTriggerUnit().getOwner()
            QuestMessageBJ(bj_FORCE_ALL_PLAYERS, bj_QUESTMESSAGE_WARNING, owner.getNameColored() + " has fired Ultimate weapon!")
            doAfter(3) ->
                let g = GetUnitsInRectMatching(bj_mapInitialPlayableArea, isStructFilter)
                for u from g
                    InstantDummyCaster.castTarget(owner, ULTIMATE_DAMAGE_SPELL_ID, 3, OrderIds.chainlightning, u)

        if GetSpellAbilityId() == GLOBAL_UW_ID
            let owner = GetTriggerUnit().getOwner()
            QuestMessageBJ(bj_FORCE_ALL_PLAYERS, bj_QUESTMESSAGE_WARNING, owner.getNameColored() + " has fired Ultimate weapon!")
            doAfter(3) ->
                let g = GetUnitsInRectMatching(bj_mapInitialPlayableArea, isStructFilter)
                for u from g
                    if u.getOwner().isEnemyOf(owner)
                        InstantDummyCaster.castTarget(owner, ULTIMATE_DAMAGE_SPELL_ID, 1, OrderIds.chainlightning, u)

        if GetSpellAbilityId() == FOCUSED_UW_ID
            let owner = GetTriggerUnit().getOwner()
            let targetOwner = GetSpellTargetUnit().getOwner()
            QuestMessageBJ(bj_FORCE_ALL_PLAYERS, bj_QUESTMESSAGE_WARNING, owner.getNameColored() + " has fired Ultimate weapon!")
            doAfter(3) ->
                let g = GetUnitsInRectMatching(bj_mapInitialPlayableArea, isStructFilter)
                for u from g
                    if u.getOwner() == targetOwner
                        InstantDummyCaster.castTarget(owner, ULTIMATE_DAMAGE_SPELL_ID, 2, OrderIds.chainlightning, u)
